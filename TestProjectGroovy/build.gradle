buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url = uri('https://www.jitpack.io')
        }
    }
    dependencies {
        classpath(files('../main/build/libs/PlumyMindustryGradlePlugin-1.0.jar'))
    }
}
plugins {
    id 'java'
}
group 'plumy.test'
version '0.1'

apply plugin: plumy.mindustry.MindustryPlugin

repositories {
    use(plumy.mindustry.BuildScript) {
        mindustryRepo()
    }
    mavenCentral()
}
sourceSets {
    main {
        java.srcDirs('src')
        resources.srcDir('resources')
    }
    test {
        java.srcDir('test')
        resources.srcDir('resources')
    }
}
mindustry {
    dependency {
        useMirror 'd7312445a1'
        arc '123fbf12b9'
    }
    client {
        be '22714'
    }
    server {
        be '22714'
    }
    mods {
        worksWith(
            repo_cyberio: 'liplum/cyberio',
            //repo_dst: 'liplum/MDT-DST',
        )
    }
    run {
        dataDir = 'temp'
    }
    assets {
        modMeta(
            name: 'test-plumy-mindustry-gradle-plugin-groovy',
            displayName: 'Test Plumy Mindustry Gradle Plugin Groovy',
            main: 'plumy.test.TestModGroovy',
            author: 'Liplum'
        )
    }
}
/*
Because tasks, 'downloadClient/Sever' and 'runClient/Server', are registered after
the project evaluation, so you shouldn't use `tasks.named('foo')` to configure it.
As an alternative, you can use `tasks.withType(Class)` instead.
tasks.named('runClient') {
    dataOnTemporary()
}
*/
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    use(plumy.mindustry.BuildScript) {
        importMindustry()
    }
}

test {
    useJUnitPlatform()
}